apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'hu.supercluster.paperwork'
apply plugin: 'com.getkeepsafe.dexcount'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 0
def versionBuild = 8

android {
    signingConfigs {
        nyuyu {
            keyAlias KEYSTORE_ALIAS
            keyPassword KEYSTORE_PASSWORD
            storeFile file(KEYSTORE_FILE_PATH)
            storePassword KEYSTORE_NYUYU_PASSWORD
        }
    }
    compileSdkVersion 24
    buildToolsVersion "24.0.2"
    defaultConfig {
        applicationId "in.nyuyu.android"
        minSdkVersion 17
        targetSdkVersion 24
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        jackOptions {
            enabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            applicationIdSuffix ".release"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.nyuyu
            minifyEnabled false
            shrinkResources false
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:percent:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha8'
    compile 'com.android.support.constraint:constraint-layout-solver:1.0.0-alpha8'

    //Play/Firebase services
    compile 'com.google.firebase:firebase-auth:9.4.0'
    compile 'com.google.firebase:firebase-database:9.4.0'
    compile 'com.jonathanfinerty.once:once:1.2.1'
    compile 'hu.supercluster:paperwork:1.2.7'

    //UI
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.jakewharton:butterknife:8.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.1.0'

    //DI
    compile 'com.google.dagger:dagger:2.5'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.5'

    //Reactive
    compile 'io.reactivex:rxjava:1.1.10'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxrelay:rxrelay:1.1.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'

    //Monitor
    compile 'com.jakewharton.timber:timber:4.3.0'

    //Monitor
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'
    //Testing
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile('com.crashlytics.sdk.android:crashlytics:2.6.3@aar') {
        transitive = true;
    }
}

paperwork {
    set = [
            twitter_key    : TWITTER_KEY,
            twitter_secret : TWITTER_SECRET,
            algolia_app_id : ALGOLIA_APP_ID,
            algolia_api_key: ALGOLIA_API_KEY,
            build_time     : buildTime(),
            git_sha        : gitSha()
    ]
}

dexcount {
    format = "list"
    includeClasses = false
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = false
    verbose = false
    maxTreeDepth = Integer.MAX_VALUE
    teamCityIntegration = false
    enableForInstantRun = false
    teamCitySlug = null
}

apply plugin: 'com.google.gms.google-services'
